apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.github.dcendents.android-maven'

android {

    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 220
        versionName "2.2.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:27.0.0'
    compile 'com.android.support:appcompat-v7:27.0.0'

    //AndroidUtilCode
    compile 'com.blankj:utilcode:1.9.6'
    //蒲公英SDK
    compile 'com.pgyersdk:sdk:2.7.6'
    //EventBus
    compile 'org.greenrobot:eventbus:3.0.0'
    //郭霖Litepal数据库
    compile 'org.litepal.android:core:1.6.0'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    //RXJava
    compile 'io.reactivex.rxjava2:rxjava:2.1.6'
    //RXAndroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //QuickAdapter
    compile 'com.github.thepacific:adapter:1.0.7'
    //BaseRecyclerViewAdapterHelper
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.33'
    //Glide
    compile 'com.github.bumptech.glide:glide:4.3.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.3.0'
    //ButterKnife
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}

//-------------------- jitPack生成文档 --------------------
tasks.withType(JavaCompile) { //指定编码
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) { //打包源码
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) { //制作文档(Javadoc)
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
